! $Id: ParentGridComponentTemplate.F90,v 1.5 2004/03/18 21:49:29 cdeluca Exp $
!
! Template code for a Gridded Component which creates 3 child Components:
!  two Gridded Components which perform a computation and a Coupler component
!  which mediates the data exchange between them.

!-------------------------------------------------------------------------
!-------------------------------------------------------------------------

!BOP
!
! !DESCRIPTION:
!  A template for a user-written Gridded Component which creates 3 child
!  Components:
!  two Gridded Components which perform a computation and a Coupler component
!  which mediates the data exchange between them.
!
!
!\begin{verbatim}

    module UserParentGridCompMod
    
    ! ESMF Framework module
    use ESMF_Mod
    
    ! User Component registration routines
    use UserGridComp1Mod, only : UserGrid1_SetServices => UserGrid_SetServices
    use UserGridComp2Mod, only : UserGrid2_SetServices => UserGrid_SetServices
    use UserCplCompMod, only : UserCpl_SetServices


    implicit none
    private
    
    public UserPComp_SetServices

    type(ESMF_GridComp), save :: comp1Grid, comp2Grid
    type(ESMF_CplComp), save :: compCoupler
    character(len=ESMF_MAXSTR), save :: gname1, gname2, cname
    type(ESMF_State), save :: G1imp, G1exp, G2imp, G2exp

    contains

    subroutine UserPComp_SetServices(gcomp, rc)
       type(ESMF_GridComp) :: gcomp
       integer :: rc

       call ESMF_GridCompSetEntryPoint(gcomp, ESMF_SETINIT, my_init, &
                                                     ESMF_SINGLEPHASE, rc)
       call ESMF_GridCompSetEntryPoint(gcomp, ESMF_SETRUN, my_run, &
                                                     ESMF_SINGLEPHASE, rc)
       call ESMF_GridCompSetEntryPoint(gcomp, ESMF_SETFINAL, my_final, &
                                                     ESMF_SINGLEPHASE, rc)

    end subroutine UserPComp_SetServices


    subroutine my_init(gcomp, importState, exportState, parentclock, rc)
      type(ESMF_GridComp) :: gcomp
      type(ESMF_State) :: importState
      type(ESMF_State) :: exportState
      type(ESMF_Clock) :: parentclock
      integer :: rc
     
      type(ESMF_DELayout) :: parentlayout
      type(ESMF_Grid) :: parentgrid

      ! call ESMF_LogErrMsg("Parent Gridded Component Initialize routine called")
      print *, "Parent Gridded Component Initialize routine called"

      ! Get the layout and grid associated with this component
      call ESMF_GridCompGet(gcomp, layout=parentlayout, grid=parentgrid, rc=rc)

      ! Create the first child Gridded component
      gname1 = "ESMF Gridded Child Component 1"
      comp1Grid = ESMF_GridCompCreate(name=gname1, layout=parentlayout, &
                                         grid=parentgrid, rc=rc)

      ! Create the second child Gridded component
      gname2 = "ESMF Gridded Child Component 2"
      comp2Grid = ESMF_GridCompCreate(name=gname2, layout=parentlayout, &
                                         grid=parentgrid, rc=rc)

      ! Create the Coupler component
      cname = "ESMF Coupler Component"
      compCoupler = ESMF_CplCompCreate(name=cname, layout=parentlayout, rc=rc)

      ! call ESMF_LogErrMsg("Component Creates finished")
      print *, "Component Creates finished"

      ! Now call the SetServices routine for each so they can register their
      ! subroutines for Init, Run, and Finalize
      call ESMF_GridCompSetServices(comp1Grid, UserGrid1_SetServices, rc)
      call ESMF_GridCompSetServices(comp2Grid, UserGrid2_SetServices, rc)
      call ESMF_CplCompSetServices(compCoupler, UserCpl_SetServices, rc)


      ! Now create Import and Export State objects in order to pass data
      ! between the Coupler and the Gridded Components
      G1imp = ESMF_StateCreate("GComp1 Import", ESMF_STATEIMPORT, gname1)
      G1exp = ESMF_StateCreate("GComp1 Export", ESMF_STATEEXPORT, gname1)

      G2imp = ESMF_StateCreate("GComp2 Import", ESMF_STATEIMPORT, gname2)
      G2exp = ESMF_StateCreate("GComp2 Export", ESMF_STATEEXPORT, gname2)

      ! Now give each of the subcomponents a chance to initialize themselves.
      call ESMF_GridCompInitialize(comp1Grid, G1imp, G1exp, parentclock, rc=rc)
      call ESMF_GridCompInitialize(comp2Grid, G2imp, G2exp, parentclock, rc=rc)

      call ESMF_CplCompInitialize(compCoupler, G1exp, G2imp, parentclock, rc=rc)

      ! call ESMF_LogErrMsg("Parent Component Initialize finished")
      print *, "Parent Component Initialize finished"

    end subroutine my_init


    subroutine my_run(gcomp, importState, exportState, parentclock, rc)
      type(ESMF_GridComp) :: gcomp
      type(ESMF_State) :: importState
      type(ESMF_State) :: exportState
      type(ESMF_Clock) :: parentclock
      integer :: rc
     
      ! call ESMF_ErrLogErrMsg("Parent Gridded Component Run routine called")
      print *, "Parent Gridded Component Run routine called"

      ! Now run the subcomponents
      call ESMF_GridCompRun(comp1Grid, G1imp, G1exp, parentclock, rc=rc)
      call ESMF_CplCompRun(compCoupler, G1exp, G2imp, parentclock, rc=rc)
      call ESMF_GridCompRun(comp2Grid, G2imp, G2exp, parentclock, rc=rc)


      ! call ESMF_LogErrMsg("Parent Component Run finished")
      print *, "Parent Component Run finished"

    end subroutine my_run


    subroutine my_final(gcomp, importState, exportState, parentclock, rc)
      type(ESMF_GridComp) :: gcomp
      type(ESMF_State) :: importState
      type(ESMF_State) :: exportState
      type(ESMF_Clock) :: parentclock
      integer :: rc
     
      ! call ESMF_ErrLogErrMsg("Parent Gridded Component Finalize routine called")
      print *, "Parent Gridded Component Finalize routine called"

      ! Give each of the subcomponents a chance to finalize themselves.
      call ESMF_GridCompFinalize(comp1Grid, G1imp, G1exp, parentclock, rc=rc)
      call ESMF_GridCompFinalize(comp2Grid, G2imp, G2exp, parentclock, rc=rc)

      call ESMF_CplCompFinalize(compCoupler, G1exp, G2imp, parentclock, rc=rc)

      ! Now remove the Components to free up their resources
      call ESMF_GridCompDestroy(comp1Grid, rc)
      call ESMF_GridCompDestroy(comp2Grid, rc)
      call ESMF_CplCompDestroy(compCoupler, rc)

      ! call ESMF_ErrLogErrMsg("Parent Gridded Component Finalize routine finished")
      print *, "Parent Gridded Component Finalize routine finished"

    end subroutine my_final

    end module UserParentGridCompMod

!\end{verbatim}
    
