#!/bin/csh -f
#
# Earth System Modeling Framework
# Copyright 2002-2003, University Corporation for Atmospheric Research, 
# Massachusetts Institute of Technology, Geophysical Fluid Dynamics 
# Laboratory, University of Michigan, National Centers for Environmental 
# Prediction, Los Alamos National Laboratory, Argonne National Laboratory, 
# NASA Goddard Space Flight Center.
# Licensed under the GPL.
#
# $Id: do_newclass,v 1.7 2003/02/28 04:21:45 eschwab Exp $
#
#===============================================================================
# do_newclass
#===============================================================================
#
# Create a new class template in the current directory
#
# Usage: do_newclass <class> <core language>> 
#
#        <class> is the name of the new class
#
#        <core language> is one of the following:
#               f - f90
#               c - C++
#        
# Template files that pertain to classes or modules have 
# "class" as the root of their filename.
#
#===============================================================================
 
if($#argv != 2) then
  echo "Usage:  do_newclass <class> <core language [c|f]>"
  exit
endif 

  set class=$1
  set lang=$2

# make lowercase classname
  set classlow = `echo $class | tr "[A-Z]" "[a-z]"`

# get component name from parent path
  set comppath = `cd ..; pwd`
  set comp = `basename $comppath`

  set DOCT_DIR=$DOC_DIR/templates

# get a copy of the source template file, rename and edit as requested class
  switch ($lang) 
  case f:
      if(!(-r ESMF_$class.F90)) then
        cat $DOCT_DIR/ESMF_class.F90 | \
        sed 's/<Class>/'$class'/g' | sed 's/<class>/'$classlow'/g' | \
        sed 's/<Comp>/'$comp'/g' > ESMF_$class.F90
      endif

      if(!(-r ../tests/ESMF_${class}Test.F90)) then
        cat $DOCT_DIR/ESMF_classTest.F90 | \
        sed 's/<Class>/'$class'/g' | sed 's/<class>/'$classlow'/g' | \
        sed 's/<Comp>/'$comp'/g' > ../tests/ESMF_${class}Test.F90
      endif
      breaksw
  case c:
      if(!(-r ESMC_$class.C)) then
        cat $DOCT_DIR/ESMC_class.C | \
        sed 's/<Class>/'$class'/g' > ESMC_$class.C
      endif

      if(!(-r ../include/ESMC_$class.h)) then
        cat $DOCT_DIR/ESMC_class.h | \
        sed 's/<Class>/'$class'/g' | sed 's/<Comp>/'$comp'/g' \
           > ../include/ESMC_$class.h
      endif

      if(!(-r ../tests/ESMC_${class}Test.C)) then
        cat $DOCT_DIR/ESMC_classTest.C | \
        sed 's/<Class>/'$class'/g' | sed 's/<class>/'$classlow'/g' | \
        sed 's/<Comp>/'$comp'/g' > ../tests/ESMC_${class}Test.C
      endif
      breaksw
  default:
    echo "Valid options are: [ c | f]"
    exit
  endsw
