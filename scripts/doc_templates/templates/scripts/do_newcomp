#!/bin/csh -f
#
# Earth System Modeling Framework
# Copyright 2002-2003, University Corporation for Atmospheric Research, 
# Massachusetts Institute of Technology, Geophysical Fluid Dynamics 
# Laboratory, University of Michigan, National Centers for Environmental 
# Prediction, Los Alamos National Laboratory, Argonne National Laboratory, 
# NASA Goddard Space Flight Center.
# Licensed under the GPL.
#
# $Id: do_newcomp,v 1.4 2002/10/14 21:53:31 cdeluca Exp $
#
#===============================================================================
# do_newcomp
#===============================================================================
#
# Create and fill a directory tree with source template files for a new
# component.
#
# Usage: do_newcomp <comp> <core language>> 
#
#        <comp> is the name of the new component, library, or module
#
#        <core language> is one of the following:
#               f - f90
#               c - C++
#        
# If a directory called <comp> doesn't already exist, it will be created in 
# the directory where the script is called.  If the <comp> directory does exist, 
# the template files needed to create the new component will be added to it.  
# Template files that relate to the entire component have <comp> as the root 
# of their filename.  Template files that pertain to classes or modules have 
# "class" as the root of their filename.
#
# E. Schwab                                                       Oct 7, 2002
#===============================================================================
 
if($#argv != 2) then
  echo "Usage:  do_newcomp <component> <core language [c|f]>"
  exit
else 
  set comp=$1
  set lang=$2
endif

# Create new component source directories if needed
  foreach dir ($comp $comp/src $comp/include $comp/interface $comp/doc $comp/examples)
    set newdir=$cwd/$dir
    if(!(-d $newdir)) then
      mkdir $newdir 
    endif 
  end

  set compdir=$cwd/$comp
  set srcdir=$cwd/$comp/src
  set intfdir=$cwd/$comp/interface
  set exdir=$cwd/$comp/examples
  set incdir=$cwd/$comp/include
  set DOCT_DIR=$DOC_DIR/templates

# Fill the source directories with template files

# note: sleeps & echos used below prevent random 0-byte file output

# makefiles
  if(!(-r $compdir/makefile)) cat $DOCT_DIR/makefile.comp | \
      sed 's/<Comp>/'$comp'/g' > $compdir/makefile; sleep 1; echo -n '.'

  if(!(-r $exdir/makefile)) cat $DOCT_DIR/makefile.examp | \
      sed 's/<Comp>/'$comp'/g' > $exdir/makefile; sleep 1; echo -n '.'

  if(!(-r $srcdir/makefile)) cat $DOCT_DIR/makefile.srcinter | \
      sed 's/<Comp>/'$comp'/g' > $srcdir/makefile; sleep 1; echo -n '.'

  if(!(-r $intfdir/makefile)) cat $DOCT_DIR/makefile.srcinter | \
      sed 's/<Comp>/'$comp'/g' > $intfdir/makefile; sleep 1; echo -n '.'

# include files
  if(!(-r $incdir/ESMF_$comp.h)) cat $DOCT_DIR/ESMF_comp.h | \
      sed 's/<Comp>/'$comp'/g' > $incdir/ESMF_$comp.h; sleep 1; echo -n '.'

  if(!(-r $incdir/ESMC_class.h)) cat $DOCT_DIR/ESMC_class.h | \
      sed 's/<Comp>/'$comp'/g' > $incdir/ESMC_class.h; sleep 1; echo -n '.'

  if(!(-r $incdir/ESMC_$comp.h)) cat $DOCT_DIR/ESMC_comp.h | \
      sed 's/<Comp>/'$comp'/g' > $incdir/ESMC_$comp.h; sleep 1; echo -n '.'

# src files
  switch ($lang) 
  case f:
      if(!(-r $srcdir/ESMF_class.F90)) cat $DOCT_DIR/ESMF_class.F90 | \
          sed 's/<Comp>/'$comp'/g' > $srcdir/ESMF_class.F90; sleep 1;
                                                             echo -n '.'

      if(!(-r $intfdir/ESMC_class.C)) cp $DOCT_DIR/ESMC_class.C $intfdir
      breaksw
  case c:
      if(!(-r $srcdir/ESMC_class.C)) cp $DOCT_DIR/ESMC_class.C $srcdir;
                                                     sleep 1; echo -n '.'

      if(!(-r $intfdir/ESMF_class.F90)) cat $DOCT_DIR/ESMF_class.F90 | \
          sed 's/<Comp>/'$comp'/g' > $intfdir/ESMF_class.F90
      breaksw
  default:
    echo "Valid options are: [ c | f]"
    exit
  endsw
  echo '.'
endif
